apply plugin: 'elasticsearch.standalone-rest-test'
apply plugin: 'elasticsearch.rest-test'

dependencies {
  testImplementation project(xpackModule('core'))
  testImplementation project(xpackProject('transport-client').path)
}

String outputDir = "${buildDir}/generated-resources/${project.name}"
def copyXPackPluginProps = tasks.register("copyXPackPluginProps", Copy) {
  from project(xpackModule('core')).file('src/main/plugin-metadata')
  // TODO revisit resolving other modules plugin properties
  from project(xpackModule('core')).tasks.pluginProperties
  into outputDir
}
project.sourceSets.test.output.dir(outputDir, builtBy: copyXPackPluginProps)

tasks.named("integTest").configure {
  systemProperty 'tests.security.manager', 'false'
}

testClusters.matching { it.name == "integTest" }.configureEach {
  testDistribution = "default"
  setting 'xpack.security.enabled', 'true'
  setting 'xpack.ml.enabled', 'false'
  setting 'xpack.license.self_generated.type', 'trial'
  user username: "test_user", password: "x-pack-test-password"
  user username: "transport", password: "x-pack-test-password", role: "transport_client"
}
